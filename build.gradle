// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

// JDK 8
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'edu.mcw.scge.dataload.ontologies.Manager'
String myAppName = 'OntologyLoad'

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'lib', include: '*.jar')

    implementation 'commons-collections:commons-collections:3.2.2'
    implementation 'org.apache.commons:commons-dbcp2:2.7.0'
    implementation 'commons-net:commons-net:3.6'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.apache.httpcomponents:httpclient:4.5.12'
    implementation 'org.springframework:spring-jdbc:5.3.0'
    implementation 'com.io7m.xom:xom:1.2.10'

    implementation 'org.biojava:biojava:5.4.0'
    implementation 'org.biojava:biojava-ontology:5.4.0'
    implementation 'org.postgresql:postgresql:9.3-1104-jdbc4'
}

jar {
    manifest {
        attributes(
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        distributionBaseName = myAppName
    }
}
task update_scgecore(type:Copy){
    from file("C:\\git\\scge-core\\build\\libs\\scge-core-1.0.jar")
    into file("lib/")

}
task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
